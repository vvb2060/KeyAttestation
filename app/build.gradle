apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

def gitCommitCount
try {
    gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
} catch(ignored) {
    gitCommitCount = 1
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId 'io.github.vvb2060.keyattestation'
        minSdkVersion 24
        targetSdkVersion 29
        versionCode gitCommitCount
        versionName '1.1.1'
        resConfigs "en", "zh-rCN", "zh-rTW"
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashReportingEnabled: "false"]
        }
        release {
            manifestPlaceholders = [crashReportingEnabled: "true"]
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = false
        viewBinding = true
    }

    packagingOptions {
        exclude 'META-INF/**'
        exclude 'kotlin/**'
        exclude 'okhttp3/**'
        exclude 'org/**'
        exclude '**.properties'
    }

    lintOptions.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

repositories {
    maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
    mavenLocal()
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    def kotlin_coroutines_version = '1.3.3'

    def androidx_core_version = "1.3.0-alpha02"
    def androidx_fragment_version = "1.2.3"
    def androidx_recyclerview_version = "1.1.0"
    def lifecycleVersion = "2.2.0"

    def rikka_appcompat_version = "1.2.0-alpha02"
    def rikka_core_version = "1.1.0"
    def rikka_material_version = "1.3.1"
    def rikka_html_version = "1.0.0"
    def rikka_recyclerview_utils_version = "1.0.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.64'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "rikka.appcompat:appcompat:$rikka_appcompat_version"
    implementation "rikka.core:core:$rikka_core_version"
    implementation "rikka.material:material:$rikka_material_version"
    implementation "rikka.html:html:$rikka_html_version"
    implementation "rikka.recyclerview:recyclerview-utils:$rikka_recyclerview_utils_version"

    implementation 'com.google.android.material:material:1.2.0-alpha05'

    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_recyclerview_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'
}

android.applicationVariants.all { variant ->
    if (variant.buildType.minifyEnabled) {
        copy {
            from 'src/main/res/values'
            into "build/generated/res/google-services/${variant.getName()}/values"
            include 'google-services.xml'
            rename { String s ->
                s.replace("google-services.xml", "values.xml")
            }
        }
    }
}