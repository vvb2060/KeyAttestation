plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def gitCommitCount
try {
    gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
} catch (ignored) {
    gitCommitCount = 1
}

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdk = 34
    buildToolsVersion = '34.0.0'
    namespace = 'io.github.vvb2060.keyattestation'
    defaultConfig {
        minSdk = 24
        targetSdk = 34
        versionCode = gitCommitCount
        versionName = '1.6.0'
        resourceConfigurations += ['en', 'zh-rCN', 'pt-rBR']
    }

    signingConfigs {
        debug {
            if (properties.getProperty("storeFile") != null) {
                storeFile file(properties.getProperty("storeFile"))
                storePassword properties.getProperty("storePassword")
                keyAlias properties.getProperty("keyAlias")
                keyPassword properties.getProperty("keyPassword")
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    packagingOptions {
        resources {
            excludes += '**'
        }
    }

    lint.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

configurations.configureEach {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'
    implementation 'com.google.guava:guava:32.1.2-android'
    implementation 'co.nstant.in:cbor:0.9'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.6.1'
    implementation 'dev.rikka.rikkax.core:core-ktx:1.4.1'
    //noinspection GradleDependency
    implementation 'dev.rikka.rikkax.material:material:1.6.6'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'

    implementation 'com.google.android.material:material:1.10.0'

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    def lifecycleVersion = "2.6.2"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    compileOnly(project(":stub"))
}
